#!/usr/bin/perl

use strict;
use warnings;

use lib "../../lib/perl";
use LinearAlgebra;

use lib "../lib";
use Utils;

#
# Unit test: LinearAlgebra::create_ref_frame
#            LinearAlgebra::find_euler_angles.
#
# Bring molecule's local frame of reference to global.
#
# Example: ./check_ref_frame "label_atom_id CA,N,CB,OG" \
#                            "Cartn_x,Cartn_y,Cartn_z" < example.cif
#

my $attribute_filter = shift;
my $attribute_select = shift;
my @cif = <>;

# Selects atom's data for further analysis.
my $selected_atom_data =
    Utils::select_atom_data( $attribute_filter,
			     $attribute_select,
			     @cif );

# Finds Euler angles and makes rotational matrices.
my @mid_atom_coord =  ( $selected_atom_data->[1][0],
		        $selected_atom_data->[1][1],
			$selected_atom_data->[1][2] );
my @up_atom_coord =   ( $selected_atom_data->[2][0],
			$selected_atom_data->[2][1],
			$selected_atom_data->[2][2] );
my @side_atom_coord = ( $selected_atom_data->[0][0],
			$selected_atom_data->[0][1],
			$selected_atom_data->[0][2] );

my @local_ref_frame =
    LinearAlgebra::create_ref_frame( @mid_atom_coord,
				     @up_atom_coord,
				     @side_atom_coord );

my ( $alpha, $beta, $gamma ) =
    LinearAlgebra::find_euler_angles( @mid_atom_coord,
				      @up_atom_coord,
				      @side_atom_coord );

# Transforms given atom coordinates and prepares data for Jmol.
print( "data \"model\"\n" );
print( scalar( grep { /ATOM/ } @cif ), "\n" );
print( "testing\n" );

my @symbols = ( "i" );

for my $cif_line ( grep( /^ATOM/, @cif ) ){
    my @cif_data = split( " ", $cif_line );
    my $atom_name = $cif_data[2];
    my @atom_coord = ( [ $cif_data[10] ],
		       [ $cif_data[11] ],
		       [ $cif_data[12] ],
		       [ 1 ] );
    my @tranf_atom_coord =
	LinearAlgebra::mult_matrix_product(
	    \@symbols,
	    LinearAlgebra::rotate_z_axis( $alpha ),
	    LinearAlgebra::rotate_x_axis( $beta ),
	    LinearAlgebra::rotate_z_axis( $gamma ),
	    LinearAlgebra::translate( ( - $mid_atom_coord[0],
					- $mid_atom_coord[1],
					- $mid_atom_coord[2] ) ),
	    \@atom_coord );
    print( "$atom_name "
    	   . "$tranf_atom_coord[0][0][0] "
    	   . "$tranf_atom_coord[0][1][0] "
    	   . "$tranf_atom_coord[0][2][0]\n" );
}

print( "end \"model\"\n" );

# Draws global axes.
print( "draw global_x_axis vector "
       . "{0 0 0} "
       . "{2 0 0}\n" );
print( "draw global_y_axis vector "
       . "{0 0 0} "
       . "{0 2 0}\n" );
print( "draw global_z_axis vector "
       . "{0 0 0} "
       . "{0 0 2}\n" );

# Colors axes.
print( "color \$global_x_axis red\n" );
print( "color \$global_y_axis green\n" );
print( "color \$global_z_axis blue\n" );
