#!/usr/bin/perl

use strict;
use warnings;

use Getopt::Long;

use ForceField::Parameters;
use LinearAlgebra qw( find_euler_angles
                      mult_matrix_product
                      translation
                      x_axis_rotation
                      z_axis_rotation );
use PDBxParser qw( extract
                   filter_new
                   obtain_atom_site );

#
# Unit test: LinearAlgebra::create_ref_frame
#            LinearAlgebra::find_euler_angles.
#
# Bring molecule's local frame of reference to global and visualizes it.
#
# Example: ./check_ref_frame -q 'label_atom_id N,CA,CB' example.cif
#

my %options;
my $target_query;
my $reference_query;

GetOptions(
    "q=s" => \$target_query,
    "r=s" => \$reference_query,
) or die "Error in command line argument.";

@ARGV = ( "-" ) unless @ARGV;

my $parameters = Parameters->new();
my $sig_figs_max = $parameters->{'_[local]_constants'}{'sig_figs_max'};

foreach my $pdbx_file ( @ARGV ) {
    my $atom_site = obtain_atom_site( $pdbx_file );

    # Selects atom's data for further analysis.
    my %target_atom_specifier = ();
    foreach ( split '&', $target_query ) {
        my ( $label, $data ) = split ' ', $_;
        $target_atom_specifier{$label} = [ split ',', $data ];
    }

    my $atom_data =
        extract( filter_new( $atom_site, { 'include' => \%target_atom_specifier } ),
                 { 'data' => [ 'Cartn_x', 'Cartn_y', 'Cartn_z' ] } );

    # Finds Euler angles and makes rotational matrices.
    my @mid_atom_coord =  ( $atom_data->[1][0],
                            $atom_data->[1][1],
                            $atom_data->[1][2] );
    my @up_atom_coord =   ( $atom_data->[2][0],
                            $atom_data->[2][1],
                            $atom_data->[2][2] );
    my @side_atom_coord = ( $atom_data->[0][0],
                            $atom_data->[0][1],
                            $atom_data->[0][2] );

    my ( $alpha, $beta, $gamma ) = @{ find_euler_angles( $parameters,
                                                         \@mid_atom_coord,
                                                         \@up_atom_coord,
                                                         \@side_atom_coord ) };

    print( "data \"model\"\n" );
    print( scalar( keys %{ $atom_site } ) + 3, "\n" );
    print( "testing\n" );

    for my $atom_id ( sort { $a <=> $b } keys %{ $atom_site } ) {
        my @atom_data = ( $atom_site->{$atom_id}{'type_symbol'},
                          $atom_site->{$atom_id}{'Cartn_x'},
                          $atom_site->{$atom_id}{'Cartn_y'},
                          $atom_site->{$atom_id}{'Cartn_z'});
        my $atom_name = $atom_data[0];
        my @atom_coord = ( [ $atom_data[1] ],
                           [ $atom_data[2] ],
                           [ $atom_data[3] ],
                           [ 1 ] );
        my ( $tranf_atom_coord ) =
            @{ mult_matrix_product(
                   [ z_axis_rotation( $alpha ),
                     x_axis_rotation( $beta ),
                     z_axis_rotation( $gamma ),
                     translation( ( - $mid_atom_coord[0],
                                    - $mid_atom_coord[1],
                                    - $mid_atom_coord[2] ) ),
                     \@atom_coord ] ) };
        printf( "%s $sig_figs_max $sig_figs_max $sig_figs_max\n",
                $atom_name,
                $tranf_atom_coord->[0][0],
                $tranf_atom_coord->[1][0],
                $tranf_atom_coord->[2][0] );
    }

    # Draws atoms on the end of axes.
    print( "X 2 0 0\nX 0 2 0\nX 0 0 2\n" );

    print( "end \"model\"\n" );

    # Draws global axes.
    print( "draw global_x_axis vector "
           . "{0 0 0} "
           . "{2 0 0}\n" );
    print( "draw global_y_axis vector "
           . "{0 0 0} "
           . "{0 2 0}\n" );
    print( "draw global_z_axis vector "
           . "{0 0 0} "
           . "{0 0 2}\n" );

    # Colors axes.
    print( "color \$global_x_axis red\n" );
    print( "color \$global_y_axis green\n" );
    print( "color \$global_z_axis blue\n" );
}
