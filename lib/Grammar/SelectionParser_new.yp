#!perl
%{
    use warnings;

    use List::MoreUtils qw( uniq );
    use List::Util qw( any );

    use AtomProperties qw( @MAINCHAIN_GRAMMAR
                           @SIDECHAIN_GRAMMAR );
    use AtomSite;
    use ConnectAtoms qw( around_distance );
%}

%left ','
%left '-'
%left 'around' 'rand'
%right 'byres' 'mainchain' 'sidechain'
%left '!'
%left '||' '&&'

%%

line
    : exp
        {
            if( ! $_[1]->[0] ) { return []; }
            if( $_[1]->[0] eq '!' ) {
                return [];
            } else {
                return [ uniq( @{ $_[1] } ) ];
            }
        }
;
# TODO: main and side should be written as exp mainchain exp, exp sidechain exp.
exp
    : 'all'
        {
            return extract( $_[0]->{USER}{ATOM_SITE},
                            {
                              'data' => [ $_[0]->{USER}{BY_ATTRIBUTE} ],
                              'is_list' => 1
                            } );
        }
    | 'mainchain' exp
        {
            filter( $_[0]->{USER}{ATOM_SITE},
                    { 'include' => { $_[0]->{USER}{BY_ATTRIBUTE} => $_[2],
                                     'label_atom_id' => \@MAINCHAIN_GRAMMAR },
                      'return' => $_[0]->{USER}{BY_ATTRIBUTE} } );
        }
    | 'sidechain' exp
        {
            filter( $_[0]->{USER}{ATOM_SITE},
                    { 'include' => { $_[0]->{USER}{BY_ATTRIBUTE} => $_[2],
                                     'label_atom_id' => \@SIDECHAIN_GRAMMAR },
                      'return' => $_[0]->{USER}{BY_ATTRIBUTE} } );
        }
    | 'byres' exp
        {
            my @residue_ids =
                uniq(
                  @{ filter( $_[0]->{USER}{ATOM_SITE},
                             { 'include' => {$_[0]->{USER}{BY_ATTRIBUTE}=>$_[2]},
                               'return' => 'label_seq_id' } ) } );
            my @chains = # TODO: look, if states might make problems.
                uniq(
                  @{ filter( $_[0]->{USER}{ATOM_SITE},
                             { 'include' => {$_[0]->{USER}{BY_ATTRIBUTE}=>$_[2]},
                               'return' => 'label_asym_id' } ) } );
            my @pdbx_model_num =
                uniq(
                  @{ filter( $_[0]->{USER}{ATOM_SITE},
                             { 'include' => {$_[0]->{USER}{BY_ATTRIBUTE}=>$_[2]},
                               'return' => 'pdbx_PDB_model_num' } ) } );
            my @alt =
                uniq(
                  @{ filter( $_[0]->{USER}{ATOM_SITE},
                             { 'include' => {$_[0]->{USER}{BY_ATTRIBUTE}=>$_[2]},
                               'return' => 'label_alt_id' } ) } );
            return filter( $_[0]->{USER}{ATOM_SITE},
                           { 'include' => { 'label_seq_id' => \@residue_ids,
                                            'label_asym_id' => \@chains,
                                            'label_alt_id' => [ @alt, '.' ],
                                            'pdbx_PDB_model_num' => \@pdbx_model_num },
                             'return' => $_[0]->{USER}{BY_ATTRIBUTE} } );
        }
    | exp '&&' exp
        {
            my @joint_ids;
            if( ! @{ $_[1] } || ! @{ $_[3] } ) { return []; }
            my @first_ids = @{ $_[1] };
            my @second_ids = @{ $_[3] };
            if( $first_ids[0] ne '!' && $second_ids[0] ne '!' ) {
                for my $id ( @first_ids ) {
                    push( @joint_ids, $id ) if any { $id eq $_ } @second_ids;
                }
                return \@joint_ids;
            } elsif( $first_ids[0] ne '!' && $second_ids[0] eq '!' ) {
                for my $id ( @first_ids ) {
                    push( @joint_ids, $id ) if ! any { $id eq $_ } @second_ids;
                }
                return \@joint_ids;
            } elsif( $first_ids[0] eq '!' && $second_ids[0] ne '!' ) {
                for my $id ( @second_ids ) {
                    push( @joint_ids, $id ) if ! any { $id eq $_ } @first_ids;
                }
                return \@joint_ids;
            } else {
                shift( @first_ids );
                shift( @second_ids );
                return [ '!', uniq( @first_ids, @second_ids ) ];
            }
        }
    | exp '||' exp
        {
            if( ! @{ $_[1] } || ! @{ $_[3] } ) {
                return [ @{ $_[1] }, @{ $_[3] } ];
            }
            if( $_[1]->[0] ne '!' && $_[3]->[0] ne '!' ) {
                return [ uniq( @{ $_[1] }, @{ $_[3] } ) ];
            } else {
                die( 'Negation (!) with or operator (|) has no valuable ' .
                     'meaning when selecting atoms' );
            }
        }
    | '(' exp ')'
        {
            $_[2];
        }
    | '!' exp
        {
            my @ids = @{ $_[2] };
            if( $ids[0] eq '!' ) {
                shift( @ids );
                return \@ids;
            } else {
                unshift( @ids, '!' );
                return \@ids;
            }
        }
    | exp 'around' NUM
        {
            my @ids = @{ $_[1] };
            my $around_atom_site;
            my $atom_specifier;
            if( $ids[0] eq '!' ) {
            #   # TODO: should look how to deal with negations.
            #   shift( @ids );
            #   return \@ids;
            } else {
                unshift( @ids, '!' );
                $around_atom_site =
                    around_distance( $_[0]->{USER}{ATOM_SITE}{_atoms},
                                     { $_[0]->{USER}{BY_ATTRIBUTE} => $_[1] },
                                     $_[3] );
                return extract( bless( { '_atoms' => $around_atom_site },
                                       'AtomSite' ),
                                {
                                  'data' => [ $_[0]->{USER}{BY_ATTRIBUTE} ],
                                  'is_list' => 1
                                } );
            }
        }
    | exp 'rand' NUM
        {
            srand( $_[0]->{USER}{SEED} );
            my @ids = @{ $_[1] };
            my @rand_ids;
            if( scalar( @ids ) < $_[3] ) {
                die( "List of $#ids atoms is smaller than the request of " .
                     "$_[3] atoms." );
            }
            foreach( 1..$_[3] ) {
                my $rand_idx = rand( $#ids );
                push( @rand_ids, $ids[$rand_idx] );
                splice( @ids, $rand_idx, 1 );
            }
            return \@rand_ids;
        }
    | 'chain' str_ope
        {
            filter( $_[0]->{USER}{ATOM_SITE},
                    { 'include' => { 'label_asym_id' => $_[2] },
                      'return' => $_[0]->{USER}{BY_ATTRIBUTE} } );
        }
    | 'resid' num_ope
        {
            filter( $_[0]->{USER}{ATOM_SITE},
                    { 'include' => { 'label_seq_id' => $_[2] },
                      'return' => $_[0]->{USER}{BY_ATTRIBUTE} } );
        }
    | 'resname' str_ope
        {
            filter( $_[0]->{USER}{ATOM_SITE},
                    { 'include' => { 'label_comp_id' => $_[2] },
                      'return' => $_[0]->{USER}{BY_ATTRIBUTE} } );
        }
    | 'atomid' num_ope
        {
            filter( $_[0]->{USER}{ATOM_SITE},
                    { 'include' => { 'id' => $_[2] },
                      'return' => $_[0]->{USER}{BY_ATTRIBUTE} } );
        }
    | 'atomname' str_ope
        {
            filter( $_[0]->{USER}{ATOM_SITE},
                    { 'include' => { 'label_atom_id' => $_[2] },
                      'return' => $_[0]->{USER}{BY_ATTRIBUTE} } );
        }
    | 'atomtype' str_ope
        {
            filter( $_[0]->{USER}{ATOM_SITE},
                    { 'include' => { 'type_symbol' => $_[2] },
                      'return' => $_[0]->{USER}{BY_ATTRIBUTE} } );
        }
    | 'altid' str_ope
        {
            filter( $_[0]->{USER}{ATOM_SITE},
                    { 'include' => { 'label_alt_id' => $_[2] },
                      'return' => $_[0]->{USER}{BY_ATTRIBUTE} } );
        }
    | 'altid' num_ope
        {
            filter( $_[0]->{USER}{ATOM_SITE},
                    { 'include' => { 'label_alt_id' => $_[2] },
                      'return' => $_[0]->{USER}{BY_ATTRIBUTE} } );
        }
    | 'model' str_ope
        {
            filter( $_[0]->{USER}{ATOM_SITE},
                    { 'include' => { 'pdbx_PDB_model_num' => $_[2] },
                      'return' => $_[0]->{USER}{BY_ATTRIBUTE} } );
        }
    | 'model' num_ope
        {
            filter( $_[0]->{USER}{ATOM_SITE},
                    { 'include' => { 'pdbx_PDB_model_num' => $_[2] },
                      'return' => $_[0]->{USER}{BY_ATTRIBUTE} } );
        }
;

num_ope
    : num_ope ',' num_ope { [ @{ $_[1] }, @{ $_[3] } ]; }
    | num_ope '-' num_ope { [ $_[1]->[0]..$_[3]->[0] ]; }
    | NUM                 { [ int( $_[1] ) ]; }
;

str_ope
    : str_ope ',' str_ope { [ @{ $_[1] }, @{ $_[3] } ]; }
    | STR                 { [ $_[1] ]; }
;

%%

sub lexer
{
    my( $parser ) = shift;

    $parser->YYData->{INPUT} || return( '',undef );
    $parser->YYData->{INPUT} =~ s/^[ \t]//;

    # Returns tokens to parser.
    for( $parser->YYData->{INPUT} ){
        if( s/^(resid)// ) {
            return ( 'resid', $1 );
        } elsif(s/^(chain)//) {
            return ( 'chain', $1 );
        } elsif(s/^(all)//) {
            return ( 'all', $1 );
        } elsif(s/^(mainchain)//) {
            return ( 'mainchain', $1 );
        } elsif(s/^(sidechain)//) {
            return ( 'sidechain', $1 );
        } elsif(s/^(byres)//) {
            return ( 'byres', $1 );
        } elsif(s/^(resname)//) {
            return ( 'resname', $1 );
        } elsif(s/^(atomid)//) {
            return ( 'atomid', $1 );
        } elsif(s/^(atomname)//) {
            return ( 'atomname', $1 );
        } elsif(s/^(atomtype)//) {
            return ( 'atomtype', $1 );
        } elsif(s/^(altid)//) {
            return ( 'altid', $1 );
        } elsif(s/^(model)//) {
            return ( 'model', $1 );
        } elsif(s/^(around)//) {
            return ( 'around', $1 );
        } elsif(s/^(rand)//) {
            return ( 'rand', $1 );
        } elsif( s/^([0-9]+)// ) {
            return ( 'NUM', $1 );
        } elsif( s/^(\w+)// ) {
            return ( 'STR', $1 );
        } elsif( s/^(,)// ) {
            return ( ',', $1 );
        } elsif( s/^(-)// ) {
            return ( '-', $1 );
        } elsif( s/^(\|\|)// ) {
            return ( '||', $1 );
        } elsif( s/^(&&)// ) {
            return ( '&&', $1 );
        } elsif( s/^(\()// ) {
            return ( '(', $1 );
        } elsif( s/^(\))// ) {
            return ( ')', $1 );
        } elsif( s/^(\!)// ) {
            return ( '!', $1 );
        }
    }
}

sub debugger
{
    exists $_[0]->YYData->{ERRMSG}

    and do {
        print $_[0]->YYData->{ERRMSG};
        return;
    };

    print "Syntax error\n";
}

sub parser
{
    my ( $self, $atom_site, $input, $by_attribute, $options ) = @_;

    my ( $seed ) = $options->{"seed"};

    $self->YYData->{INPUT} = $input;
    $self->YYData->{ATOM_SITE} = $atom_site;
    $self->YYData->{BY_ATTRIBUTE} = $by_attribute;
    $self->YYData->{SEED} = $seed;

    my $selected_atom_data = $self->YYParse( yylex => \&lexer,
                                             yyerror => \&debugger,
                                             yydebug=>0x00 );
    my $selected_atom_site =
        filter( $atom_site,
                {
                  "atom_site" => $atom_site,
                  "include" => { $by_attribute => $selected_atom_data }
                } );
    $selected_atom_site->pdbx();
    # return $selected_atom_site;
}
