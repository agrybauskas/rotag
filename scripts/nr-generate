#!/usr/bin/perl

use strict;
use warnings;

use Getopt::Long;
use List::MoreUtils qw( uniq );

use LinearAlgebra qw( pi );
use PDBxParser qw( filter
                   obtain_atom_site
                   to_pdbx );

use PseudoAtoms qw( generate_library );
use Grammar::CommandLineParser;

#*nr-generate
#*    generate rotamer library.
#*
#*USAGE:
#*    nr-generate --input input.cif > output.cif
#*
#*OPTIONS:
#*    --input <file> | <stdin>
#*        PDBx file.
#*    --model <str> (default: rotation_only)
#*        rules (model) that will be used to move side-chains.
#*    --angle-step <float> (default: 0.1) (in radians)
#*        angle that each dihedral angle is turned by.
#*    --potential (default: leonard_jones | hard_sphere | soft_sphere |
#*                 exponential | combined)
#*        uses potential energy function for determining interactions between
#*        selected residues and atoms that are reachable or described.
#*    --cutoff <float> (default: 0.4)
#*        the maximum value of energy potential that allows for further rotamer
#*        search.
#*    --version
#*        print version.

# --------------------------------- Options ----------------------------------- #

my %options;

my $version = "1.0.0";

GetOptions(
    "input=s" => \$options{"pdbx_file"},
    "model=s" => \$options{"conf_model"},
    "potential=s" => \$options{"interactions"},
    "angle-step=s" => \$options{"small_angle"},
    "cutoff=s" => \$options{"cutoff"},
    "help" => sub { open( my $fh, "<", $0 );
		    while( <$fh> ) {
			my ( $help_message ) = $_ =~ m/^#(\*.*)/;
			$help_message =~ s/^\*$/ /g if $help_message;
			$help_message =~ s/^\*//g if $help_message;
			print( $help_message, "\n" ) if $help_message; }
		    close( $fh );
		    exit; },
    "version" => sub { print $version, "\n";
		       exit; } )
    or die "Error in command line argument.";

# Default options. # TODO: should make defaults according to selected potentials.
$options{"conf_model"} = "rotation_only" unless $options{"conf_model"};
$options{"interactions"} = "leonard_jones" unless $options{"interactions"};
$options{"small_angle"} = 0.1 unless $options{"small_angle"};
$options{"cutoff"} = 0.4 unless $options{"cutoff"};

# ----------------------------------- Main ------------------------------------ #

# Reads file path, stdin and "-" operator.
my $atom_site;
if( $options{"pdbx_file"} ) {
    $atom_site = obtain_atom_site( $options{"pdbx_file"} );
} elsif( ! defined $options{"pdbx_file"} && ! -t STDIN ) {
    $atom_site = obtain_atom_site( '-' );
} else {
    die( "Use command \"nr-generate --help\" in order to see the manual.\n" );
}

my $selected_atom_site =
    filter( { "atom_site" => $atom_site,
	      "include" => { "[local]_selection_state" => [ "T", "S" ] } } );
my @residue_ids =
    uniq(
	@{ filter( { "atom_site" => $selected_atom_site,
		     "include" => { "[local]_selection_state" => [ "T" ] },
		     "data" => [ "label_seq_id" ],
		     "is_list" => 1 } ) } );

# Starts generating rotamer library.
my $rotamer_library =
    generate_library( { "atom_site" => $selected_atom_site,
			"residue_ids" => \@residue_ids,
			"small_angle" => $options{"small_angle"} * pi(),
			"conf_model" => $options{"conf_model"},
			"interactions" => $options{"interactions"},
			"cutoff" => $options{"cutoff"} } );

my %pdbx_loops; # PDBx loop data structure (see PDBxParser.pm to_pdbx)

# Collects residue angle data.
# TODO: should not forget adding atoms that make up dihedral angles and energy
# entry.
my $id = 1;
my $rotamer_id = 1;
$pdbx_loops{"_[local]_rotamer_angle"}{"attributes"} =
    [ "id", "rotamer_id", "residue_id", "residue_type", "type", "value" ];

for my $residue_id ( sort { $a cmp $b } keys %{ $rotamer_library } ) {
    my @residue_name =
    	uniq( @{ filter( { "atom_site" => $selected_atom_site,
    			   "data" => [ "label_comp_id" ],
    			   "is_list" => 1 } ) } );
    for my $rotamer ( @{ $rotamer_library->{$residue_id} } ) {
    	for my $angle_name ( sort { $a cmp $b } keys %{ $rotamer->{"angles"} } ){
    	    my $angle_value = $rotamer->{"angles"}{$angle_name};
    	    push( @{ $pdbx_loops{"_[local]_rotamer_angle"}{"data"} },
    		  $id );
    	    push( @{ $pdbx_loops{"_[local]_rotamer_angle"}{"data"} },
    		  $rotamer_id );
    	    push( @{ $pdbx_loops{"_[local]_rotamer_angle"}{"data"} },
    		  $residue_id );
    	    push( @{ $pdbx_loops{"_[local]_rotamer_angle"}{"data"} },
    		  $residue_name[0] );
    	    push( @{ $pdbx_loops{"_[local]_rotamer_angle"}{"data"} },
    		  $angle_name );
    	    push( @{ $pdbx_loops{"_[local]_rotamer_angle"}{"data"} },
    		  sprintf( "%.3f", $angle_value ) );
    	    $id++;
    	}
    	$rotamer_id++;
    }
}

to_pdbx( { "atom_site" => $selected_atom_site,
	   "add_atom_attributes" => [ "[local]_selection_state" ],
	   "pdbx_loops" => \%pdbx_loops } );
